AC_PREREQ(2.52)dnl
AC_REVISION([1.0])dnl

dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([include/meteo.h])
AC_CANONICAL_TARGET([])
AM_INIT_AUTOMAKE(meteo, 0.7.0)
AM_CONFIG_HEADER(include/config.h)

dnl add support for libtool
dnl AC_LIBLTDL_CONVENIENCE
dnl AC_SUBST(INCLTDL)
dnl AC_SUBST(LIBLTDL)
dnl AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
dnl AC_CHECK_LIB(dl, dlopen)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_ARG_VAR(MSGFMT, [name of msgfmt program from gettext package])
AC_CHECK_PROG(MSGFMT, msgfmt, msgfmt)

dnl Checks for libraries.
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(resolv, res_query)
AC_CHECK_LIB(m, atan2)

dnl the following libraries are required, so configure should fail if
dnl it does not find them
AC_CHECK_LIB(mysqlclient, mysql_query, ,
       [echo "the mysql client library is required, fix your include dirs"
	echo "e.g. using CPPFLAGS, or install MySQL from http://www.mysql.org"
	exit 1])
AC_CHECK_LIB(gd, gdImageCreate, ,
       [echo "The GD library is required, get it from http://www.boutell.com"
	exit 1])
AC_CHECK_LIB(z, gzopen, ,
       [echo "zlib is required for PNG"
	exit 1])
AC_CHECK_LIB(png, png_read_image, ,
       [echo "the PNG library is required, get it from http://www.png.org"
	exit 1])

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h mysql/mysql.h sys/msg.h sys/ipc.h alloca.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(strerror)

dnl Check for the XML library
XML_CONFIG=xml2-config
AC_MSG_CHECKING(looking for xml2 library)
if ${XML_CONFIG} --libs print > /dev/null 2>&1
then
	XMLVERS=`${XML_CONFIG} --version`
	LIBXML_LIBS=`${XML_CONFIG} --libs`
	LIBXML_CFLAGS=`${XML_CONFIG} --cflags`
	AC_SUBST(LIBXML_LIBS)
	AC_SUBST(LIBXML_CFLAGS)
	AC_MSG_RESULT(${XMLVERS} found)
else
	AC_MSG_ERROR([Could not find libxml anywhere, check ftp://xmlsoft.org/])
fi
CFLAGS="${LIBXML_CFLAGS} ${CFLAGS}"
CXXFLAGS="${LIBXML_CXXFLAGS} ${CXXFLAGS}"
LIBS="${LIBXML_LIBS} ${LIBS}"

dnl default configuration directory
METEOCONFFILE=${sysconfdir}/meteo.xml
AC_SUBST(METEOCONFFILE)

dnl set the directory for the installation of the browser
BROWSERDIR=""
AC_ARG_WITH(browser-dir,
	[  --with-browser-dir      directory to install the meteo browser])
BROWSERDIR="${with_browser_dir}"
AC_SUBST(BROWSERDIR)

dnl set the directory for the installation of the HTML pages
HTMLDIR=""
AC_ARG_WITH(html-dir,
	[  --with-html-dir         directory to install the HTML pages])
HTMLDIR="${with_html_dir}"
AC_SUBST(HTMLDIR)

CONFIGDATE=`date +"%B %d, %Y" | tr 'A-Z' 'a-z'`
AC_SUBST(CONFIGDATE)

AC_CONFIG_FILES([Makefile include/Makefile lib/Makefile src/Makefile
support/Makefile conf/Makefile doc/Makefile html/Makefile wunderground/Makefile
browser/Makefile browser/po/Makefile
db/Makefile man/Makefile man/meteo.1 html/meteo.html])
AC_OUTPUT
